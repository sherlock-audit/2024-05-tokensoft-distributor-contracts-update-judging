Melted Citron Squirrel

high

# `voteFactor`/`fractionDenominator` set to incorrect value

## Summary
`voteFactor`/`fractionDenominator` set to incorrect value

## Vulnerability Detail
Let's compare the initialization of `PerAddressTrancheVestingInitializable.sol` and `PerAddressContinuousVestingInitializable.sol` :

`PerAddressTrancheVestingInitializable.sol`:
```solidity
    ) internal onlyInitializing {
        __AdvancedDistributor_init(
            _token,
            _total,
            _uri,
            10000, // 1x voting power
            10000, // vested fraction
            _maxDelayTime,
            _salt,
            _owner
        );
    }
```

`PerAddressContinuousVestingInitializable.sol`:
```solidity
    ) internal onlyInitializing {
        __AdvancedDistributor_init(
            _token,
            _total,
            _uri,
            10000, // 1x voting power
            10 ** 18, // provides the highest resolution possible for continuous vesting
            _maxDelayTime,
            _salt,
            _owner
        );
    }
```
As you can see they initialize the advanced distributor with different values for `fractionDenominator`:
```solidity
    function __AdvancedDistributor_init(
        IERC20 _token,
        uint256 _total,
        string memory _uri,
        uint256 _voteFactor,
        uint256 _fractionDenominator, //<<<<<< 10000 or 10 ** 18
        uint160 _maxDelayTime,
        uint160 _salt,
        address _owner
    ) internal onlyInitializing {
        _setSweepRecipient(payable(_owner));
        voteFactor = _voteFactor;
        emit SetVoteFactor(voteFactor);

        __Distributor_init(_token, _total, _uri, _fractionDenominator);
        __FairQueue_init(_maxDelayTime, _salt);
    }
```

Because of this we get completely different calculations in `AdvancedDistributorInitializable: tokensToVotes()`:
```solidity
    /**
     * convert a token quantity to a vote quantity
     */
    function tokensToVotes(uint256 tokenAmount) private view returns (uint256) {
        return (tokenAmount * voteFactor) / fractionDenominator;
    }
```
Let's compare both calculations by using the same `tokenAmount = 5e18`.
`PerAddressTrancheVestingInitializable.sol` sets `voteFactor` to `10000` and `fractionDenominator` to `10000`:
> (5e18 * 10000) / 10000 = 5e18

`PerAddressContinuousVestingInitializable.sol` sets `voteFactor` to `10000` and `fractionDenominator` to `10 ** 18`:
> (5e18 * 10000) / 1e18 = 50000

The `tokensToVotes()` function is used to determine how much votes to mint/burn in `_reconcileVotingPower()`:
```solidity
    function _reconcileVotingPower(address beneficiary) private {
        // current potential voting power
        uint256 currentVotes = balanceOf(beneficiary);
        // correct voting power after initialization, claim, or adjustment
        DistributionRecord memory record = records[beneficiary];
        uint256 newVotes = record.claimed >= record.total ? 0 : tokensToVotes(record.total - record.claimed);

        if (currentVotes > newVotes) {
            // reduce voting power through ERC20Votes extension
            _burn(beneficiary, currentVotes - newVotes);
        } else if (currentVotes < newVotes) {
            // increase voting power through ERC20Votes extension
            _mint(beneficiary, newVotes - currentVotes);
        }
    }
```
Because of the minting and burning I assume that `tokensToVotes()` should return an 18 decimal number which it does not in the second calculation.

Even if we use a `tokenAmount = 5` that is not 18 decimal instead of `5e18` the calculation still doesn't seem right:
> (5 * 10000) / 1e18 = 0

There is no logic for `tokensToVotes` to return both 5e18 votes or 50000 votes

## Impact
Incorrect precision for votes

## Code Snippet
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/main/contracts/packages/hardhat/contracts/claim/factory/PerAddressTrancheVestingInitializable.sol#L11-L29
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/main/contracts/packages/hardhat/contracts/claim/factory/PerAddressContinuousVestingInitializable.sol#L10-L28
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/main/contracts/packages/hardhat/contracts/claim/factory/AdvancedDistributorInitializable.sol#L59-L82
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/main/contracts/packages/hardhat/contracts/claim/factory/AdvancedDistributorInitializable.sol#L85-L99

## Tool used

Manual Review

## Recommendation
Change the `fractionDenominator` or `voteFactor` to an appropriate value
