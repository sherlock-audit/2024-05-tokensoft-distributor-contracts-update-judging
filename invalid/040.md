Bouncy Magenta Nightingale

high

# Incorrect require statement can cause user to be unable to claim

## Summary
Incorrect require statement can cause user to be unable to claim
## Vulnerability Detail
The owner can call `AdvancedDistributorInitializable#adjust()` in order to adjust the amount that a user can claim:
```solidity
    function adjust(address beneficiary, int256 amount) external onlyOwner {
        DistributionRecord memory distributionRecord = records[beneficiary];
        require(distributionRecord.initialized, "must initialize before adjusting");

        uint256 diff = uint256(amount > 0 ? amount : -amount);
        require(diff < type(uint120).max, "adjustment > max uint120");

        if (amount < 0) {
            // decreasing claimable tokens
            require(total >= diff, "decrease greater than distributor total");
            require(distributionRecord.total >= diff, "decrease greater than distributionRecord total");
            total -= diff;
            records[beneficiary].total -= uint120(diff);
            token.safeTransfer(owner(), diff);
        } else {
            // increasing claimable tokens
            total += diff;
            records[beneficiary].total += uint120(diff);
        }
        _reconcileVotingPower(beneficiary);
        emit Adjust(beneficiary, amount);
    }
```
The issue here is in this  __require statement__:
```solidity
      require(distributionRecord.total >= diff, "decrease greater than distributionRecord total");
```
The __require statement__ should actually check for the unclaimed tokens and not the total tokens of a particular user.

Imagine the following scenario:
1. Bob and Alice have 100 tokens each that are already fully available to claim
2. The value of the `total` variable is equal to 200 tokens and the contract has the needed amount of tokens, 200 tokens
3. The owner decides to adjust the tokens Bob can claim to 0
4. He calls `adjust()` with the right input (address of Bob and -100 tokens) but Bob frontruns him and takes out his tokens
5. The function goes into the first __if block__:
```solidity
if (amount < 0) {
            // decreasing claimable tokens
            require(total >= diff, "decrease greater than distributor total");
            require(distributionRecord.total >= diff, "decrease greater than distributionRecord total");
            total -= diff;
            records[beneficiary].total -= uint120(diff);
            token.safeTransfer(owner(), diff);
        }
```
6. The first __require statement__ passes as 200 is larger than 100
7. The second __require statement__ passes as 100 is equal to 100
8. The value of the `total` variable is now 100 (200 - 100) and the total claimable tokens for Bob is 0 (100 - 100)
9. 100 tokens are transferred to the owner because of the last line

Now, the contract balance is 0 as Bob already took out his 100 tokens and the owner got the other 100 tokens because the __require statement__ should have actually checked for the unclaimed tokens of Bob and reverted instead of checking for his total claimable tokens. Alice tries to withdraw her 100 tokens but since the balance of the contract is 0, she will not be able to.
## Impact
Incorrect require statement can cause user to be unable to claim
## Code Snippet
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/67edd899612619b0acefdcb0783ef7a8a75caeac/contracts/packages/hardhat/contracts/claim/factory/AdvancedDistributorInitializable.sol#L124-L145
## Tool used

Manual Review

## Recommendation
Make the following change:
```diff
- require(distributionRecord.total >= diff, "decrease greater than distributionRecord total");
+ require(distributionRecord.total - distributionRecord.claimed >= diff, "decrease greater than the unclaimed beneficiary tokens");
```