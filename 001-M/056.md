Mysterious Mandarin Crow

high

# claim function in PerAddressTrancheVestingMerkleDistributor.sol will always revert thus causing user to never claim their tokens.

## Summary
claim function in PerAddressTrancheVestingMerkleDistributor.sol will always revert because of the hardcoded bytes(0) value passed to the _executeclaim function. Note this finding is different from the #2 because these functions are in two different contracts, therefore different areas of code where there is a bug. Also their impact is also different .

## Vulnerability Detail
Following is claim function in PerAddressTrancheVestingMerkleDistributor.sol
```solidity
 function claim(
        uint256 index, // the beneficiary's index in the merkle root
        address beneficiary, // the address that will receive tokens
        uint256 totalAmount, // the total claimable by this beneficiary
        bytes32[] calldata merkleProof
    )
        external
        validMerkleProof(keccak256(abi.encodePacked(index, beneficiary, totalAmount)), merkleProof)
        nonReentrant
    {
        // effects
        uint256 claimedAmount = _executeClaim(beneficiary, totalAmount, new bytes(0));
        // interactions
        _settleClaim(beneficiary, claimedAmount);
    }
```
Note that the third parameter in _executeClaim function is new bytes(0)
_executeClaim function is as follows
```solidity
 function _executeClaim(address beneficiary, uint256 totalAmount, bytes memory)
        internal
        virtual
        override
        returns (uint256 _claimed)
    {
        _claimed = super._executeClaim(beneficiary, totalAmount, new bytes(0));
        _reconcileVotingPower(beneficiary);
    }
```
In above function it hardcodes bytes(0) in the _executeClaim function in the parent contract which is as follows
```solidity
 function _executeClaim(address beneficiary, uint256 _totalAmount, bytes memory data) internal virtual returns (uint256) {
        uint120 totalAmount = uint120(_totalAmount);

        // effects
        if (records[beneficiary].total != totalAmount) {
            // re-initialize if the total has been updated
            _initializeDistributionRecord(beneficiary, totalAmount);
        }

        uint120 claimableAmount = uint120(getClaimableAmount(beneficiary, data));
        require(claimableAmount > 0, "Distributor: no more tokens claimable right now");

        records[beneficiary].claimed += claimableAmount;
        claimed += claimableAmount;

        return claimableAmount;
    }
```
Claimable amount is calculated as follows
   uint120 claimableAmount = uint120(getClaimableAmount(beneficiary, data)); = uint120(getClaimableAmount(beneficiary, 0)) with bytes 0 as the data field.

Now getClaimableAmount function is as follows
```solidity
function getClaimableAmount(address beneficiary, bytes memory data) public view virtual returns (uint256) {
        require(records[beneficiary].initialized, "Distributor: claim not initialized");

        DistributionRecord memory record = records[beneficiary];

        uint256 claimable = (record.total * getVestedFraction(beneficiary, block.timestamp, data)) / fractionDenominator;
        return record.claimed >= claimable
            ? 0 // no more tokens to claim
            : claimable - record.claimed; // claim all available tokens
    }
```
Now vested fraction is calulated using getVestedFraction. Note that data passed is equal to bytes(0). getVestedFraction function is as follows
```solidity
function getVestedFraction(address beneficiary, uint256 time, bytes memory data) public view override returns (uint256) {
        Tranche[] memory tranches = abi.decode(data, (Tranche[]));

        uint256 delay = getFairDelayTime(beneficiary);
        for (uint256 i = tranches.length; i != 0;) {
            unchecked {
                --i;
            }

            if (time - delay > tranches[i].time) {
                return tranches[i].vestedFraction;
            }
        }

        return 0;
    }
```
Now as the data passed is equal to 0. The above function will return 0 .
Therefore
 uint256 claimable = (record.total * getVestedFraction(beneficiary, block.timestamp, data)) / fractionDenominator; = 0
I.e claimable = 0 now getClaimable amount function returns the following 
```solidity
return record.claimed >= claimable
            ? 0 // no more tokens to claim
            : claimable - record.claimed;
```
As claimable was zero and no amount is claimed yet it would return 0.

The following check was followed in the _executeClaim function
```solidity
uint120 claimableAmount = uint120(getClaimableAmount(beneficiary, data));
        require(claimableAmount > 0, "Distributor: no more tokens claimable right now");
```
Now as we have seen that the getClaimableAmount would return 0 therefore the above check would fail and revert . Thus not allowing the users to claim their tokens.




## Impact
Users not able to claim their tokens. Therefore loss of funds for the users. As there is loss of funds this a high severity issue.
## Code Snippet
https://github.com/sherlock-audit/2024-05-tokensoft-distributor-contracts-update/blob/67edd899612619b0acefdcb0783ef7a8a75caeac/contracts/packages/hardhat/contracts/claim/factory/PerAddressTrancheVestingMerkleDistributor.sol#L62
## Tool used

Manual Review

## Recommendation
Firstly the claim function in PerAddressTrancheVestingMerkleDistributor.sol needs to modified as there is no tranches variable passed to the claim function.

```solidity
function claim(
        uint256 index, // the beneficiary's index in the merkle root
        address beneficiary, // the address that will receive tokens
        uint256 totalAmount, // the total claimable by this beneficiary
==>       Tranche[] calldata tranches,
        bytes32[] calldata merkleProof
    )
        external
        validMerkleProof(keccak256(abi.encodePacked(index, beneficiary, totalAmount)), merkleProof)
        nonReentrant
    {
  ==>      bytes memory data = abi.encode(tranches);
        // effects
        uint256 claimedAmount = _executeClaim(beneficiary, totalAmount, data); <==
        // interactions
        _settleClaim(beneficiary, claimedAmount);
    }
```
next the _executeClaim function should be changed as follows
```solidity
function _executeClaim(address beneficiary, uint256 totalAmount, bytes memory data) <==
        internal
        virtual
        override
        returns (uint256 _claimed)
    {
        _claimed = super._executeClaim(beneficiary, totalAmount, data); <==
        _reconcileVotingPower(beneficiary);
    }
```
